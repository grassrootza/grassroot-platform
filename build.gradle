buildscript {
	ext {
		springBootVersion = '2.1.3.RELEASE'
	}

	repositories {
		gradlePluginPortal()
	}

	dependencies {
		classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
	}
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'io.spring.dependency-management'

	group = 'za.org.grassroot'
	sourceCompatibility = 1.8
    version = '1.3.0'

	repositories {
        mavenLocal()
        maven { url "http://repo.maven.apache.org/maven2" }
        maven { url "http://optimizely.bintray.com/optimizely" }
        maven { url "https://dl.bintray.com/grassrootza/grassroot-graph" }
	}

    task downloadDependencies() {
      description 'Download all dependencies to the Gradle cache'
      doLast {
        configurations.findAll { it.canBeResolved }.files
      }
    }

	dependencyManagement {
		imports {
			mavenBom "org.springframework.boot:spring-boot-dependencies:$springBootVersion"
		}
	}

    test {
      exclude '**/*IT*'
      testLogging {
        events "failed"
        exceptionFormat = "full"
      }

      systemProperty "grassroot.sms.gateway", "127.0.0.1"
      systemProperty "grassroot.sms.gateway.username", "grassrootstest"
      systemProperty "grassroot.sms.gateway.password", "test"
    }

    dependencies {

      compile 'com.google.guava:guava:19.0'
      compile 'io.springfox:springfox-swagger2:2.7.0'
      compile 'io.springfox:springfox-swagger-ui:2.7.0'

      annotationProcessor 'org.projectlombok:lombok'
      compileOnly 'org.projectlombok:lombok'
      testAnnotationProcessor 'org.projectlombok:lombok'
      testCompileOnly 'org.projectlombok:lombok'

      testCompile 'junit:junit'
      testCompile 'org.dbunit:dbunit:2.5.3'
      testCompile 'com.github.springtestdbunit:spring-test-dbunit:1.3.0'

      compile 'org.springframework.boot:spring-boot-starter'
      testCompile 'org.springframework.boot:spring-boot-starter-test'
    }
}
