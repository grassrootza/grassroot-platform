buildscript {
	ext {
		springBootVersion = '2.1.3.RELEASE'
	}

	repositories {
		gradlePluginPortal()
	}

	dependencies {
		classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
	}
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'io.spring.dependency-management'

	group = 'za.org.grassroot'
	sourceCompatibility = 1.8
    version = '1.2.4'

	repositories {
        mavenLocal()
        maven { url "http://repo.maven.apache.org/maven2" }
        maven { url "http://optimizely.bintray.com/optimizely" }
        maven { url "https://dl.bintray.com/grassrootza/grassroot-graph" }
	}

    task downloadDependencies() {
      description 'Download all dependencies to the Gradle cache'
      doLast {
        configurations.findAll { it.canBeResolved }.files
      }
    }

	dependencyManagement {
		imports {
			mavenBom "org.springframework.boot:spring-boot-dependencies:$springBootVersion"
		}
	}

    test {
      exclude '**/*IT*'
      testLogging {
        events "failed"
        exceptionFormat = "full"
      }

      systemProperty "grassroot.sms.gateway", "127.0.0.1"
      systemProperty "grassroot.sms.gateway.username", "grassrootstest"
      systemProperty "grassroot.sms.gateway.password", "test"
    }

    dependencies {

      compile group: 'com.google.guava', name: 'guava', version: '19.0'
      compile group: 'io.springfox', name: 'springfox-swagger2', version: '2.7.0'
      compile group: 'io.springfox', name: 'springfox-swagger-ui', version: '2.7.0'

      compile group: 'org.projectlombok', name: 'lombok'

      testCompile group: 'junit', name: 'junit'
      testCompile group: 'org.dbunit', name: 'dbunit', version: '2.5.3'
      testCompile group: 'com.github.springtestdbunit', name: 'spring-test-dbunit', version: '1.3.0'

      compile('org.springframework.boot:spring-boot-starter')
      testCompile('org.springframework.boot:spring-boot-starter-test')
    }
}
